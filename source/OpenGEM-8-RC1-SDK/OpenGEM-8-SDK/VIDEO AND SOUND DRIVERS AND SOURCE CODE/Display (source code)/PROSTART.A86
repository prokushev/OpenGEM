;/*	PROSTART.A86	05/03/84 -  01/05/85	Lee Lorenzen		*/
;/*	added an SSEG with 32 words 01/25/85	Gregg Morris		*/
;/*	added check for 'GEMAES'    01/29/85	Tom Rolander		*/
;
GLOBAMNT	equ	0
;

	cseg
;
	EXTRN	GEMAIN:NEAR
	extrn	PROGEND:word		;/* link proend.obj last	*/
					;/*   this allows us to free	*/
					;/*   memory so the memory alloc*/
					;/*   in RSLIB.C will work	*/
;
	PUBLIC	MAIN
MAIN:
	cli
	mov	ax,seg stk
	mov	ds,ax
	mov	ss,ax
	mov	sp,offset stk
	sti
;
;	free up any memory we're not using
;		es points at our psp
;
	mov	ax,es			;es has our psp
	mov	PARABEG,ax
	mov	ax,offset PROGEND
	add	ax, GLOBAMNT
	add	ax,15
	mov	cl,4
	shr	ax,cl
	mov	cx,ax			; cx = our data size in paragraphs
	mov	bx,ds
	add	bx,cx
	mov	PARAEND,bx
;
;
free_ex:
	mov	ax,PARABEG
	mov	es,ax
	mov	bx,PARAEND
	sub	bx,ax
	mov	ah,4ah
	int	21h
	mov	dx,offset memerrmsg
	jc	errormsg
;
;
; check for presence of GEMAES before calling the application
	xor	ax,ax
	mov	es,ax
	mov	bx,es:.3bch
	mov	ax,es:.3beh
	mov	es,ax
	mov	di,2
	add	di,bx
	mov	si,offset gemaesmsg
	mov	cx,6
	rep	cmpsb
	jz	runapplication
	mov	dx,offset gemaesmsg
errormsg:
	mov	ah,9
	int	21h
	mov	dx,offset terminmsg
	mov	ah,9
	int	21h
	jmp	terminate

runapplication:
	mov	ax,ds
	mov	es,ax
	call	GEMAIN

terminate:
	mov	ax,04c00h
	int	21h
;
;
	sseg
 	rw	32
;
	dseg
PARABEG	dw	0
PARAEND	dw	0

gemaesmsg db	'GEMAES not present in memory.'
	db	'$'
memerrmsg db	'Unable to free memory.'
	db	'$'
terminmsg db	0dh,0ah
	db	'Execution terminated.'
	db	'$'

	rw	1024
stk	dw	0
	end
